/* 전체 프로젝트 의존성 관리 */
buildscript {
    ext {
        springBootVersion = '2.3.4.RELEASE'
        springfoxVersion = '2.9.2'
        swaggerVersion = '1.5.21'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
    }
}


allprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    group 'kr.seok'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

}
subprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    group 'kr.seok'
    version '1.0-SNAPSHOT'
    sourceCompatibility = '1.8'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        /* SpringFramework */
        implementation 'org.springframework.boot:spring-boot-starter-batch'
        implementation "org.springframework.boot:spring-boot-starter-data-jpa"
        implementation "javax.validation:validation-api"

        // 컴파일 단계에서 이미 롬복의 기능을 제공받고, 런타임 단계에서는 해당 라이브러리가 필요하지 않기 때문에 Compile Only로 설정
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        runtimeOnly 'com.h2database:h2'
        runtimeOnly 'mysql:mysql-connector-java'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.batch:spring-batch-test'
    }

    test {
        useJUnitPlatform()
    }
}

/* Querydsl 프로젝트 리스트 */
def querydslProjects = [
        project(":batch-hospital"),
        project(":batch-admin")
]

configure(querydslProjects) {

    apply plugin: "io.spring.dependency-management"

    dependencies {
        compileOnly "com.querydsl:querydsl-jpa"
        compileOnly "com.querydsl:querydsl-core"
        annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
        // querydsl JPAAnnotationProcessor 사용 지정
        annotationProcessor "jakarta.persistence:jakarta.persistence-api:2.2.3"
        annotationProcessor "jakarta.annotation:jakarta.annotation-api:1.3.5"
    }

    // querydsl 적용
    def generated = 'src/main/generated'

    sourceSets {
        main.java.srcDirs += [generated]
    }

    tasks.withType(JavaCompile) {
        options.annotationProcessorGeneratedSourcesDirectory = file(generated)
    }

    clean.doLast {
        file(generated).deleteDir()
        file('out').deleteDir()
    }

}

project(":batch-common") {
    dependencies {
        implementation "org.apache.poi:poi:5.1.0"          // .xlsx
        implementation "org.apache.poi:poi-ooxml:5.1.0"    // .xls

        implementation 'org.springframework.boot:spring-boot-starter-mail'
    }
    /* DTO, Util 클래스 등등 */
    bootJar { enabled = false }
    jar { enabled = true }
}

project(":batch-core") {
    /* 서비스 비즈니스 로직과 관계가 없음, 하나의 인프라 스트럭쳐에 대한 책임만 갖는다. (DB Server, Redis, ElasticSearch 등등) */
    dependencies {
        implementation project(":batch-common")
    }
}

project(":batch-admin") {
    /* API용 비즈니스 로직관련 모듈 swagger */
    dependencies {
        implementation project(":batch-common")
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

        implementation "io.springfox:springfox-swagger2:${springfoxVersion}"
        implementation "io.springfox:springfox-swagger-ui:${springfoxVersion}"

        implementation "io.swagger:swagger-annotations:${swaggerVersion}"
        implementation "io.swagger:swagger-models:${swaggerVersion}"
    }
}

project(":batch-hospital") {
    dependencies {
        implementation project(":batch-common")
//        implementation 'org.apache.httpcomponents:httpclient:4.5.13'
//        /* https://mvnrepository.com/artifact/org.modelmapper.extensions/modelmapper-spring */
//        implementation 'org.modelmapper.extensions:modelmapper-spring:1.1.0'
//
//        implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
//
//        // es 의존성 추가시 배치가 끝나지 않는 문제 발생
//        implementation 'org.elasticsearch:elasticsearch:7.6.2'
//        implementation 'org.elasticsearch.client:elasticsearch-rest-client:7.6.2'
//        implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.6.2'
    }
}

project(":batch-hospital-es") {
    dependencies {
        implementation project(":batch-common")
        implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

        implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
        // https://mvnrepository.com/artifact/org.elasticsearch/elasticsearch
        implementation 'org.elasticsearch:elasticsearch:7.6.2'
        implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.6.2'
    }
}

project(":batch-library") {
    dependencies {
        implementation project(":batch-common")
    }
}

project(":batch-library-jpa") {
    dependencies {
        implementation project(":batch-common")
    }
}

project(":batch-library-test") {
    dependencies {
        implementation project(":batch-common")
    }
}

project(":batch-real-estate") {
    dependencies {
        implementation project(":batch-common")
    }
}

project(":batch-mail-service") {
    dependencies {
        implementation project(":batch-common")
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    }
}
